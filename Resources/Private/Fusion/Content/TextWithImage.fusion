prototype(Kleisli.Kubbar:Content.TextWithImage) < prototype(Neos.Neos:ContentComponent) {

    class = "flex flex-col md:flex-row gap-8"
    textClass = "px-4 md:px-0 prose hyphens-auto"
    imageClass = ""

    @private {
        textClass = Neos.Fusion:Join {
            givenClass = ${props.textClass}
            position = ${node.properties.imagePosition == 'right' ? ' order-1' : ' order-2'}
            width = Neos.Fusion:Match {
                @subject = ${node.properties.ratio}
                25-75 = "w-full md:w-1/4"
                33-66 = "w-full md:w-1/3"
                50-50 = "w-full md:w-1/2"
                66-33 = "w-full md:w-2/3"
                75-25 = "w-full md:w-3/4"
            }
            @glue = ' '
        }
        imageClass = Neos.Fusion:Join {
            givenClass = ${props.imageClass}
            position = ${node.properties.imagePosition == 'right' ? ' order-2' : ' order-1'}
            width = Neos.Fusion:Match {
                @subject = ${node.properties.ratio}
                25-75 = "w-full md:w-3/4"
                33-66 = "w-full md:w-2/3"
                50-50 = "w-full md:w-1/2"
                66-33 = "w-full md:w-1/3"
                75-25 = "w-full md:w-1/4"
            }
            @glue = ' '
        }
    }

    renderer = afx`
        <div class={props.class}>
            <div class={private.textClass}>
                <Neos.Neos:Editable property="text" />
            </div>
            <Kleisli.Kubbar:Content.Image class={private.imageClass} />
        </div>
    `
}
